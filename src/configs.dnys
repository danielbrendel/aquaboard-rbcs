# AquaShell Counter-Strike Retroboard config sources manager script file

const CONFIGS_WINDOW_POSX int <= 600;
const CONFIGS_WINDOW_POSY int <= 135;
const CONFIGS_WINDOW_WIDTH int <= 400;
const CONFIGS_WINDOW_HEIGHT int <= 500;
const CONFIGS_WINDOW_BORDER_SIZE int <= 6;

global hConfigFont int;

array arrConfigItems string 1 (
    "polyoffset"
);

function loadConfigItem void(configBasePath string)
{
    local iRndConfigItem int;
    local configContent string;

    random 1 arrConfigItems.length iRndConfigItem;
    -- iRndConfigItem;

    sys {type "%configBasePath\%arrConfigItems[%iRndConfigItem].txt"} configContent;
    s_tokenize "%configContent" "%CR%LF" arrSplittedConfigLines;

    for (j, 0, %arrSplittedConfigLines.count, -inc) {
        s_replace arrSplittedConfigLines[%j] {\n} " ";
    };
    
    dx.gfx.loadfont "Verdana" 8 20 hConfigFont;
};

function drawConfigLines void()
{
    local curposx int;
    local curposy int;
    local strcurline string;
    local curlinelen int;
    local cmtpos int;
    local curcolorr int;
    local curcolorg int;
    local curcolorb int;
    local curcolora int;

    set curposx <= %CONFIGS_WINDOW_POSX;
    set curposy <= %CONFIGS_WINDOW_POSY;

    += curposx 20;
    += curposy 20;

    dx.gfx.drawfilledbox %CONFIGS_WINDOW_POSX %CONFIGS_WINDOW_POSY %CONFIGS_WINDOW_WIDTH %CONFIGS_WINDOW_HEIGHT 62 70 55 150;
    dx.gfx.drawbox %CONFIGS_WINDOW_POSX %CONFIGS_WINDOW_POSY %CONFIGS_WINDOW_WIDTH %CONFIGS_WINDOW_HEIGHT %CONFIGS_WINDOW_BORDER_SIZE 79 89 69 150;

    for (i, 0, %arrSplittedConfigLines.count, -inc) {
        set strcurline <= "%arrSplittedConfigLines[%i]";

        set curcolorr <= 255;
        set curcolorg <= 255;
        set curcolorb <= 255;
        set curcolora <= 255;

        s_getlen "%strcurline" curlinelen;
        if (%curlinelen, -gr, 1) {
            s_ltrim strcurline;

            s_find "%strcurline" "//" cmtpos;
            if (%cmtpos, -gre, 0) {
                set curcolorr <= 150;
                set curcolorg <= 190;
                set curcolorb <= 150;
                set curcolora <= 255;
            } else {
                set curcolorr <= 255;
                set curcolorg <= 255;
                set curcolorb <= 255;
                set curcolora <= 255;
            };
        };

        dx.gfx.drawstring %hConfigFont "%strcurline" %curposx %curposy %curcolorr %curcolorg %curcolorb %curcolora;

        += curposx 0;
        += curposy 30;
    };
};