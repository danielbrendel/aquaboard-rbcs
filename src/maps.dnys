# AquaShell Counter-Strike Retroboard maps showcase manager script file

const MAPS_WINDOW_POSX int <= 30;
const MAPS_WINDOW_POSY int <= 520;
const MAPS_TIMER_DELAY int <= 10000;

global iCurrentIndex int;
global szCurrentMap string;
global hTxtBar int;
global hCurMapSpr int;
global hMapsFont int;

array arrMapsItems string 10 (
    "aim_ak_colt",
    "aim_map",
    "awp_map",
    "de_rats",
    "fun_allinone",
	"fy_iceworld",
    "fy_pool_day",
    "he_glasswar",
    "ka_legoland",
    "scoutzknivez"
);

array arrMapSprites int 0 ();

function SwitchMapShowcase_OnElapsed bool()
{
	call updateMapShowcase() => void;

	result true;
};

function loadMapShowcase void(spriteBasePath string)
{
    local tempHandle int;

    for (mpcnt, 0, %arrMapsItems.length, -inc) {
        dx.gfx.loadsprite "%spriteBasePath\maps\%arrMapsItems[%mpcnt].png" 1 350 197 1 false tempHandle;
        array_item_append arrMapSprites "%tempHandle";
    };

    dx.gfx.loadsprite "%spriteBasePath\bar.png" 1 350 32 1 false hTxtBar;
    dx.gfx.loadfont "Verdana" 8 17 hMapsFont;

    random 0 %arrMapsItems.length iCurrentIndex;
    call updateMapShowcase() => void;

    aut_addtimer SwitchMapShowcase %MAPS_TIMER_DELAY;
};

function updateMapShowcase void()
{
    local fixedquantity int;

    set fixedquantity <= %arrMapsItems.length;
    -- fixedquantity;

    ++ iCurrentIndex;
    if (%iCurrentIndex, -gre, %fixedquantity) {
        set iCurrentIndex <= 0;
    };

    array_item_get arrMapsItems %iCurrentIndex szCurrentMap;
    array_item_get arrMapSprites %iCurrentIndex hCurMapSpr;
};

function drawMapShowcase void()
{
    local barposy int;
    local txtposx int;
    local txtposy int;

    set barposy <= %MAPS_WINDOW_POSY;
    set txtposx <= %MAPS_WINDOW_POSX;
    set txtposy <= %MAPS_WINDOW_POSY;

    += barposy 8;
    += txtposx 23;
    += txtposy 16;
    
    dx.gfx.drawsprite %hCurMapSpr %MAPS_WINDOW_POSX %MAPS_WINDOW_POSY 0 0.0 0.9 false 0 0 0 0;
    dx.gfx.drawsprite %hTxtBar %MAPS_WINDOW_POSX %barposy 0 0.0 0.9 false 0 0 0 0;
    dx.gfx.drawstring %hMapsFont "%szCurrentMap" %txtposx %txtposy 255 255 255 255;
};

function checkMapClicked void()
{
    local chkmapframewidth int;
	local chkmapframeheight int;
	
	set chkmapframewidth <= %MAPS_WINDOW_POSX;
	set chkmapframeheight <= %MAPS_WINDOW_POSY;

	+= chkmapframewidth 315;
	+= chkmapframeheight 175;

	if (%iMouseXPos, -gr, %MAPS_WINDOW_POSX) {
		if (%iMouseXPos, -ls, %chkmapframewidth) {
			if (%iMouseYPos, -gr, %MAPS_WINDOW_POSY) {
				if (%iMouseYPos, -ls, %chkmapframeheight) {
                    call updateMapShowcase() => void;
					dx.snd.play %hSndLinkClick %GLOBAL_VOLUME;
				};
			};
		};
	};
};